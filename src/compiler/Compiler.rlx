/*
==========================================
	Backing compiler state
==========================================
*/


struct Label {
	i32 LabelNumber,
	i32 OffsetToReplace,
	i8 IsPlaceholder
}

struct CompilerState {
	BuildConfig* Config,
	ParserState* pState,
	FunctionDefine* CurrentFunction,
	
	union {
		/* Output file builders */
		
		PEBuilder* PE,
		ELFBuilder* ELF
	},
	
	/* Address (in loaded output file) where globals will live */
	i32 GlobalBase,
	
	/* Buffer containing assembled (but not linked) code */
	i8* CodeBuffer,
	i32 CodeBufferSize,
	i32 Index,
	
	/* Array of relocations */
	i32* CodeRelocations,
	i32 CodeRelocationCount,
	
	/* Record of label definitions/usages inside of CodeBuffer, for actually linking labels */
	Label** Labels,
	i32 LabelCount,
	i32 LabelBufferCapacity,
	
	i32 LabelIndex,
	
	/* Labels for `continue` and `break` statements to jump to */
	i32 CurrentLoopContinueLabel,
	i32 CurrentLoopBreakLabel,
	
	/* Label for the current function's buffer of strings used */
	i32 CurrentFunctionStringsLabel,
	
	/* Next free register number for expression evaluation/whatever */
	i8 RegisterStackIndex,
	
	/* Flags to ensure that certain registers are not clobbered */
	i8 NoClobberCD89,
	i32 ParameterRegistersMaxSave,
	i32 ParameterRegistersMinSave,
	i8 NoClobberA,
	
	/* Flags the correct return sequence for `Main()` is used when Config->OutputFormat = OUTPUT_ELF */
	i8 CompilingMain,
	
	/* Count of `Try` statements logically 'wrapping' the current area in code 
		With loops resetting ScopedTryDepth for Continue/Break
	*/
	i32 ScopedTryDepth,
	i32 TotalTryDepth
	
	define void Initialize(BuildConfig* Config, ParserState* pState) {
		this->Config := Config
		this->pState := pState
		
		this->CodeBufferSize := 0x10000
		this->CodeBuffer := Alloc(0x10000) As i8*
		this->Index := 0
		
		this->CodeRelocations := Alloc(8) As i64*
		this->CodeRelocationCount := 0
		
		this->Labels := Alloc(80) As Label**
		this->LabelCount := 0
		this->LabelBufferCapacity := 10
		
		this->LabelIndex := (pState->FunctionCount) + 1
		this->RegisterStackIndex := 0
		
		this->ParameterRegistersMaxSave := 0
		this->ParameterRegistersMinSave := 0
		
		this->CurrentFunction := null
		
		this->ScopedTryDepth := 0
		this->TotalTryDepth := 0
	}
	define void Free() {
		Free(this->CodeBuffer)
		Free(this->CodeRelocations)
		Free(this->Labels)
	}
}

/*
==========================================
	High-ish level compiling functions, along with global/function dumping
==========================================
*/

#Require "./src/compiler/ASTOptimize.rlx"
#Require "./src/compiler/CodeGen.rlx"

declare void CompileImportStub(CompilerState*, FunctionDefine*)
declare void CompileFunction(CompilerState*, FunctionDefine*, i8)

define void DumpGlobals(CompilerState* cState) {
	/* Walk the globals hashmap and print the offset/size of each */
	ParserState* pState := cState->pState
	
	VariableHashMap* Globals := pState~>Globals
	HashMap* RawGlobals := Globals->Map
	
	void* Elements := RawGlobals->Elements
	
	i32 MaxOffset := Globals->NextOffset
	i32 Base := cState->GlobalBase
	
	i32 MaxIndex := RawGlobals->BufferMaxCount
	
	for (i32 ElementIndex := 0, ElementIndex < MaxIndex, ElementIndex += 1) {
		if (Elements[ElementIndex]) {
			HashMapElement* FoundElement := Elements[ElementIndex] As HashMapElement*
			
			while (FoundElement) {
				VariableInfo* NextGlobal := FoundElement->Value
				
				if (NextGlobal->IsGlobal = GLOBAL_IS_OPTIMIZED_AWAY) {
					Print("^60     Opimized Away    = ")
				}
				else {
					i32 ThisOffset := NextGlobal->StackOffset
					i32 ThisSize := GetTypeStorageSize(NextGlobal->Type)
					
					i32 Start := Base + ThisOffset
					i32 End := Start + ThisSize
					
					Print("%x:%x = ", Start, End)
				}
				
				NextGlobal->Name->PrintLine()
				
				ResetColors()
				
				FoundElement := FoundElement->NextElement
			}
		}
	}
}

define void PrintFunction(CompilerState* cState, FunctionDefine* TargetFunction, i32 FunctionOffset, i64 CODE_BASE, i8 IsOverload) {
	/* Print a function in the format:
			Address Of Function - Size of function (Call count of function) Name
		With the type signature of the function optionally printed afterwards, if the function is overloaded.
	*/
	
	i8* Format := "%x - %i	(%i)	"
	
	if (TargetFunction->CallCount = 0) {
		Format := "^60%x - %i	(%i)	"
	}
	
	Print(Format, CODE_BASE + FunctionOffset, cState->Index - FunctionOffset, TargetFunction->CallCount)
	
	if (TargetFunction->IsMethod) {
		PrintSafeString(TargetFunction->MethodOfTypeName)
		
		if (TargetFunction->IsMethod = FUNCTION_IS_STATIC_METHOD) {
			PrintCharacter(':')
		}
		else {
			PrintCharacter('.')
		}
	}
	
	PrintSafeString(TargetFunction->Name)
	
	if (IsOverload) {
		PrintCharacter('(')
		
		for (i32 ParameterIndex := 0, ParameterIndex < TargetFunction->ParameterCount, ParameterIndex += 1) {
			VariableInfo* NextParameter := TargetFunction->Parameters[ParameterIndex]
			
			if (NextParameter->IsVariadic) {
				PrintLiteral('...')
			}
			else {
				PrintType(NextParameter->Type)
			}
			
			if (ParameterIndex + 1 != TargetFunction->ParameterCount) {
				PrintLiteral(', ')
			}
		}
		
		PrintCharacter(')')
	}
	
	Print("^F0\n")
}

define i8 CompileOverload(BuildConfig* Config, CompilerState* cState, FunctionDefine* BaseFunction, i8 IsOverload, i64 CODE_BASE, i32* MainOffset) {
	/* Compiles either:
		A single function (BaseFunction)
		Or an entire linked list of function overloads, starting with BaseFunction
	*/
	
	i32 FunctionOffset := cState->Index
	i8 Result := false
	
	if (BaseFunction->IsImported) {
		if (Config->OutputFormat = OUTPUT_PE && BaseFunction->CallCount != 0) {
			CompileImportStub(cState, BaseFunction)
		}
	}
	else if !(BaseFunction->HasFullDefinition) {
		ContextError(BaseFunction~>Context, "Function is declared, but never defined.")
	}
	else {
		i8 IsMain := SafeStringEqualsAString(BaseFunction->Name, "Main")
		
		if (IsMain) {
			/* If we found the main function, let the callee know for linking's sake. */
			
			Result := true
			MainOffset[0] := FunctionOffset
		}
		
		if (BaseFunction->CallCount != 0) {
			CompileFunction(cState, BaseFunction, IsMain)
		}
	}
	
	if (Config->DumpInfo & DUMP_FUNCTIONS) {
		/* If we're dumping a function in an overload chain, then print it as an overload
			Or, if we're dumping a single function and it has `->Next` set, then we've just started an overload chain
			 and still need to print it as an overload
		*/
		
		PrintFunction(cState, BaseFunction, FunctionOffset, CODE_BASE, !!BaseFunction->Next || IsOverload)
	}
	
	if (BaseFunction->Next) {
		/* Recursively compile an entire overload chain */
		
		Result |= CompileOverload(Config, cState, BaseFunction->Next, true, CODE_BASE, MainOffset)
	}
	
	return Result
}

define i8 CompileFunctionHashMap(BuildConfig* Config, CompilerState* cState, HashMap* Map, i64 CODE_BASE, i32* MainOffset) {
	/* Compiles a hashmap containing FunctionDefine*'s as if it were a linear array 
		Returns true if the main function was found
	*/
	
	i8 Result := false
	void* Elements := Map->Elements
	
	for (i32 ElementIndex := 0, ElementIndex < (Map->BufferMaxCount), ElementIndex += 1) {
		if (Elements[ElementIndex]) {
			HashMapElement* FoundElement := Elements[ElementIndex] As HashMapElement*
			
			while (FoundElement) {
				FunctionDefine* NextFunction := (FoundElement->Value) As FunctionDefine*
				
				Result |= CompileOverload(Config, cState, NextFunction, false, CODE_BASE, MainOffset)
				
				/* Update the current element to be the next element in the linked list */
				FoundElement := FoundElement->NextElement
			}
		}
	}
	
	return Result
}

define void CompilerStart(BuildConfig* Config, ParserState* pState, i8* OutputFilePath) {
	CountReferences(pState)
	EliminateGlobals(pState~>Globals)
	
	CompilerState C
	
	C.Initialize(Config, pState)
	
	i64 CODE_BASE := 0x3000
	
	if (Config->OutputFormat = OUTPUT_BIN) {
		C.GlobalBase := Config->ImageBase + 0x10
		
		CODE_BASE := Config->ImageBase + pState~>Globals->NextOffset + 0x10
	}
	else if (Config->OutputFormat = OUTPUT_ELF) {
		C.ELF := BuildELFBuilder()
		
		C.GlobalBase := Config->ImageBase + 64 + 0x1000
		
		CODE_BASE := pState~>Globals->NextOffset + 564 + 0x2000
	}
	else if (Config->OutputFormat = OUTPUT_PE) {
		C.PE := BuildBuilder()
		
		C.PE->ImportFixupBase := C.CodeBuffer
		C.PE->PE->ImageBase := Config->ImageBase
		
		C.GlobalBase := Config->ImageBase + C.PE->NextRVA
	}
	
	CompilerState* cState := &C

	if (Config->DumpInfo & DUMP_GLOBALS) {
		DumpGlobals(cState)
	}
	
	i32 MainOffset := 0
	
	; Compile all global functions
	i8 FoundMain := CompileFunctionHashMap(Config, cState, pState~>Functions, CODE_BASE, &MainOffset)
	
	; Loop through each custom type
	HashMap* Types := pState~>Types
	
	i32 ElementCount := Types->BufferMaxCount
	void* Elements := Types->Elements
	
	for (i32 ElementIndex := 0, ElementIndex < ElementCount, ElementIndex += 1) {
		if (Elements[ElementIndex]) {
			HashMapElement* FoundElement := Elements[ElementIndex] As HashMapElement*
			
			while (FoundElement) {
				TypeInfo* NextType := (FoundElement->Value) As TypeInfo*
				
				/* If the type is a struct compile any methods */
				if (NextType->IsStructType) {
					CompileFunctionHashMap(Config, cState, NextType~>Methods, CODE_BASE, &MainOffset)
					CompileFunctionHashMap(Config, cState, NextType~>StaticMethods, CODE_BASE, &MainOffset)
				}
				
				FoundElement := FoundElement->NextElement
			}
		}
	}
	
	if !(MainOffset) {
		SwapOutputStream()
		Print("^50Main function not found, aborting.^F0")
		Exit(1)
	}
	
	ResolveAllLabels(cState)
	
	if (Config->DumpInfo & DUMP_FUNCTIONS) {
		Print("^10	Globals use %i bytes\n	%i total bytes of code generated.^F0\n", pState~>Globals->NextOffset, cState->Index)
	}
	
	FileDelete(OutputFilePath)
	
	/* Actual compiling is done, time to build the output file */
	
	i64 OutputFileHandle := FileOpen(OutputFilePath, FILE_READ | FILE_WRITE | FILE_CREATE_NEW)
	
	if (OutputFileHandle < 0) {
		SwapOutputStream()
		Print("Could not open output file, error code: %i\nAborting\n", FileGetError(OutputFileHandle))
		Exit(1)
	}
	
	i32 Result := 0
	i32 BytesWritten := 0
	
	if (Config->OutputFormat = OUTPUT_BIN) {
		i32 CodeBase := pState~>Globals->NextOffset + 0x10
		i32 FileSize := CodeBase + cState->Index
		
		void* Binary := Alloc(FileSize)
		i8* BinaryBytes := Binary As i8*
		
		BinaryBytes[0] := 0xE9
		((Binary + 1) As i32*)[0] := CodeBase + MainOffset - 5
		((Binary + 8) As i64*)[0] := Config->ImageBase
		
		for (i32 CodeIndex := 0, CodeIndex < cState->Index, CodeIndex += 1) {
			BinaryBytes[CodeBase + CodeIndex] := cState->CodeBuffer[CodeIndex]
		}
		
		Result := FileWrite(OutputFileHandle, Binary, FileSize)
		BytesWritten := FileSize
	}
	else if (Config->OutputFormat = OUTPUT_ELF) {
		AddDataSection(cState->ELF, pState~>Globals->NextOffset)
		AddCodeSection(cState->ELF, cState->CodeBuffer, cState->Index)
		
		FinalizeELFBuilder(cState->ELF, MainOffset, Config->ImageBase)
		
		Result := FileWrite(OutputFileHandle, cState->ELF->Buffer, cState->ELF->BufferSize)
		BytesWritten := cState->ELF->BufferSize
	}
	else if (Config->OutputFormat = OUTPUT_PE) {
		AddData(cState->PE, pState~>Globals->NextOffset)
		
		i64 TextRVA := AddCode(cState->PE, cState->CodeBuffer, cState->Index, MainOffset)
		
		; for (i32 RelocationIndex := 0, RelocationIndex < (cState->CodeRelocationCount), RelocationIndex += 1) {
		; 	i32 NextRelocationOffset := (cState->CodeRelocations)[RelocationIndex]
		; 	
		; 	i32 PageRVA := TextRVA + (NextRelocationOffset / 0x1000)
		; 	i16 OffsetInPage := (NextRelocationOffset % 0x1000) As i16
		; 	
		; 	/* Todo: Decide if a 32 bit relocation is correct or not. As long as imagebase is 32 bits, this is fine for now */
		; 	
		; 	AddRelocation(cState->PE, PageRVA, OffsetInPage, false)
		; }
		
		FinalizeBuilder(cState->PE)
		
		Result := FileWrite(OutputFileHandle, cState->PE->Buffer, cState->PE->BufferSize)
		BytesWritten := cState->PE->BufferSize
	}
	
	if (Result < 0) {
		SwapOutputStream()
		Print("Error writing output file, error code: %i\nAborting\n", FileGetError(Result))
		Exit(1)
	}
	
	Print("^10Done writing output file, %i bytes written, file format contains %i bytes of overhead^F0\n", BytesWritten, BytesWritten - cState->Index)
	
	FileClose(OutputFileHandle)
}

define i8 StackIndexToRegister(i8 Index) {
	if (Index = 4) {
		return R13
	}
	else if (Index > 4) {
		return Index + 9 As i8
	}
	else {
		return Index + 8 As i8
	}
}



define i8 PushRegisterStack(CompilerState* cState) {
	i8 Index := cState->RegisterStackIndex
	
	i8 TrueIndex := Index % 7
	i8 Result := StackIndexToRegister(TrueIndex)
	
	if (Index >= 8) {
		EmitPush_R64(cState, Result)
	}

	cState->RegisterStackIndex += 1
	
	return Result
}
define i8 TopOfRegisterStack(CompilerState* cState) {
	return StackIndexToRegister(cState->RegisterStackIndex)
}
define i8 PopRegisterStack(CompilerState* cState) {
	cState->RegisterStackIndex -= 1
	
	i8 Index := cState->RegisterStackIndex
	
	i8 TrueIndex := Index % 7
	i8 Result := StackIndexToRegister(TrueIndex)
	
	if (Index >= 8) {
		EmitPop_R64(cState, Result)
	}
	
	return Result
}

define void EnsureCodeBufferSize(CompilerState* cState) {
	if ((cState->Index + 0x100) >= (cState->CodeBufferSize)) {
		cState->CodeBufferSize += 0x5000
		cState->CodeBuffer := ReAlloc(cState->CodeBuffer, cState->CodeBufferSize)
		
		if (cState->Config->OutputFormat = OUTPUT_PE) {
			cState->PE->ImportFixupBase := cState->CodeBuffer
		}
	}
}

define void AddCodeRelocation(CompilerState* cState, i32 Offset) {
	i32 Index := cState->CodeRelocationCount
	
	cState->CodeRelocations := ReAlloc(cState->CodeRelocations, (Index + 1) * 8)
	(cState->CodeRelocations)[Index] := Offset
	
	cState->CodeRelocationCount += 1
}


/*
==========================================
	Variable helpers
==========================================
*/

define i32 GetVariableOffset(CompilerState* cState, VariableInfo* TargetVariable, i8* FitsDisp8) {
	i32 Offset := TargetVariable->StackOffset
	
	if !(TargetVariable->IsGlobal) {
		if (cState->Config->DebugMode & DEBUG_EMIT_FUNCTION_NAMES) {
			Offset += 0x10
		}
	}
	
	if (FitsDisp8 != null) {
		FitsDisp8[0] := (Offset <= 112)
	}
	
	if (TargetVariable->IsGlobal) {
		return Offset
	}
	
	return -(Offset + 8)
}

/* Todo: Merge these three functions into one */

define void CompileGetVariableAddress(CompilerState* cState, i8 ResultRegister, VariableInfo* TargetVariable) {
	i8 UseDisp8 := false
	i32 Offset := GetVariableOffset(cState, TargetVariable, &UseDisp8)
	
	i8 BaseRegister := RBP
	
	if (TargetVariable->IsGlobal) {
		BaseRegister := R15
	}
	
	if (UseDisp8) {
		EmitLea_R64_R64_DISP8(cState, ResultRegister, BaseRegister, Offset As i8)
	}
	else {
		EmitLea_R64_R64_DISP32(cState, ResultRegister, BaseRegister, Offset)
	}
	
	if (BaseRegister = R15) {
		AddCodeRelocation(cState, (cState->Index) - (!UseDisp8 * 3) + 1)
	}
}

declare void CompileExpression(CompilerState*, i8, ASTNode*)

define void CompileGetVariableValue(CompilerState* cState, i8 ResultRegister, VariableInfo* TargetVariable) {
	i8 TargetSize := GetTypeSize(TargetVariable->Type) As i8
	
	if (TargetVariable->Type.IsArray) {
		return CompileGetVariableAddress(cState, ResultRegister, TargetVariable)
	}
	
	if (TargetVariable->IsGlobal = GLOBAL_IS_OPTIMIZED_AWAY) {
		return CompileExpression(cState, ResultRegister, TargetVariable->LastValue)
	}
	
	if (GetPointerDepth(TargetVariable->Type) = 0 && GetBaseType(TargetVariable->Type)->IsStructType) {
		/* If GetVariableValue is called on a struct, then we should actually result in a pointer to that struct
			So Compile(Get|Set)StructField can depending on getting a pointer, and never the struct as a number
			Any time this could be abused *should* be caught by type checking 
		*/
		
		CompileGetVariableAddress(cState, ResultRegister, TargetVariable)
	}
	else {
		i8 UseDisp8 := false
		i32 Offset := GetVariableOffset(cState, TargetVariable, &UseDisp8)
		
		i8 BaseRegister := RBP
		
		if (TargetVariable->IsGlobal) {
			BaseRegister := R15
		}
		
		;if (UseDisp8) {
		;	EmitMove_RX_RIX_DISP8(cState, ResultRegister, BaseRegister, Offset As i8, TargetSize)
		;}
		;else {
		;	EmitMove_RX_RIX_DISP32(cState, ResultRegister, BaseRegister, Offset, TargetSize)
		;}
		
		EmitMove_RX_RIX_DISPX(cState, ResultRegister, BaseRegister, Offset, UseDisp8, TargetSize)
		
		if (BaseRegister = R15) {
			AddCodeRelocation(cState, (cState->Index) - (!UseDisp8 * 3) + 1)
		}
	}
}
define void CompileSetVariableValue(CompilerState* cState, VariableInfo* TargetVariable, i8 SourceRegister) {
	i8 TargetSize := GetTypeSize(TargetVariable->Type) As i8
	
	i8 UseDisp8 := false
	i32 Offset := GetVariableOffset(cState, TargetVariable, &UseDisp8)
	
	i8 BaseRegister := RBP
	
	if (TargetVariable->IsGlobal) {
		BaseRegister := R15
	}
	
	if (UseDisp8) {
		EmitMove_RIX_DISP8_RX(cState, BaseRegister, SourceRegister, Offset As i8, TargetSize)
	}
	else {
		EmitMove_RIX_DISP32_RX(cState, BaseRegister, SourceRegister, Offset, TargetSize)
	}
	
	if (BaseRegister = R15) {
		AddCodeRelocation(cState, (cState->Index) - (!UseDisp8 * 3) + 1)
	}
}


/*
==========================================
	Actual compile functions
==========================================
*/

define void Cast(CompilerState* cState, i8 Register, i8 Size) {
	EmitMove_R64_RX(cState, Register, Register, Size)
}

define void CompileImportStub(CompilerState* cState, FunctionDefine* FunctionToCompile) {
	/* Compiles an imported function by emitting a jump to the actual function */
	
	SetLabel(cState, FunctionToCompile->LabelNumber)
	
	EmitMove_R64_I64(cState, RBX, 0)
	
	AddImport(cState->PE, FunctionToCompile->ImportFileName, FunctionToCompile->ImportFunctionName, (cState->Index) - 8)
	AddCodeRelocation(cState, cState->Index - 8)
	
	EmitJump_RI64(cState, RBX)
}

declare void CompileBlock(CompilerState*, Block*)
declare void CompileAssembly(CompilerState*, AssemblyBlock*)

define void CompileFunction(CompilerState* cState, FunctionDefine* FunctionToCompile, i8 IsMain) {
	/* Compiles a single function */
	
	cState->RegisterStackIndex := 0
	
	SetLabel(cState, FunctionToCompile->LabelNumber)
	
	if (FunctionToCompile->Attribute = KEYWORD_ASSEMBLY) {
		/* Assembly functions should have no extra code generated, just whatever the asm block contains. */
		
		CompileAssembly(cState, FunctionToCompile->AssemblyBody)
		return 0
	}
	
	if (FunctionToCompile->Body->StatementCount = 0) {
		/* Empty functions don't deserve any prelude code. */
		
		EmitReturn(cState)
		return 0
	}
	
	cState->CurrentFunction := FunctionToCompile
	
	i8 USE_SYSTEMV := true /* cState->OutputELF */
	
	if (FunctionToCompile->Attribute = KEYWORD_X64) {
		; Force a function to be compiled using the x64 ABI
		USE_SYSTEMV := false
	}
	
	/* Assume 4 register parameters, and 0 stack parameters */
	i8 MAX_REGISTER_PARAMETERS := 4
	
	if (USE_SYSTEMV) {
		MAX_REGISTER_PARAMETERS := 6
	}
	
	i8 ParameterCount := FunctionToCompile->ParameterCount
	i8 RegisterParameterCount := MAX_REGISTER_PARAMETERS
	i8 StackParameterCount := 0
	
	if (ParameterCount < MAX_REGISTER_PARAMETERS) {
		/* If there are less than 4/6 parameters, then the number of register parameters is just the number of parameters */
		
		RegisterParameterCount := ParameterCount
	}
	else if (ParameterCount > MAX_REGISTER_PARAMETERS) {
		/* Else if there are 5/7+ parameters, the stack parameter count is (TotalParamCount - MAX_REGISTER_PARAMETERS) */
		
		StackParameterCount := ParameterCount - MAX_REGISTER_PARAMETERS
		
		/* Also, store RSP in RBX for extracting the stack parameters with smaller offsets */
		EmitMove_R64_R64(cState, RBX, RSP)
	}
	
	/* Set up the stack frame for this function */
	i16 LocalSpace := FunctionToCompile~>Locals->NextOffset
	i16 LocalSpaceRemainder := LocalSpace % 16
	
	if (LocalSpaceRemainder) {
		LocalSpace += 8
	}
	
	EmitPush_R64(cState, RBP)
	EmitMove_R64_R64(cState, RBP, RSP)
	
	if (IsMain && cState->Config->OutputFormat = OUTPUT_ELF  && ParameterCount <= 2) {
		/* When compiling main for SystemV, load the first two parameter registers (if they exist) 
			With ArgC and ArgV loaded from the stack since:
				RBP + 0 = ArgC
				RBP + 1..ArgC = ArgV
			so mov rdi, [rsp+8] will get argc, and
			lea rsi, [rsp+16] will get argv
			which are then moved into the local ArgC/ArgV variables by the default parameter saving behavior
		*/
		
		;EmitMove_RX_RIX_DISP8(cState, RDI, RBP, 8, 8)
		EmitMove_RX_RIX_DISPX(cState, RDI, RBP, 8, true, 8)
		EmitLea_R64_R64_DISP8(cState, RSI, RBP, 16)
	}
	
	i32 StringsStart := NextLabel(cState)
	
	if (cState->Config->DebugMode & DEBUG_EMIT_FUNCTION_NAMES) {
		EmitMove_R32_I32(cState, RAX, 0x12345678)
		EmitPush_R64(cState, RAX)
		EmitLea_R64_Label(cState, RAX, StringsStart)
		EmitPush_R64(cState, RAX)
		
		; Extend LocalSpace to account for extra offset from RBP to local variables
		LocalSpace += 0x8
	}
	
	if (LocalSpace) {
		/* Only emit an instruction to allocate space for locals when we have locals */
		EmitSub_R64_IX(cState, RSP, LocalSpace)
	}
	
	for (i8 RegisterParameterIndex := 0, RegisterParameterIndex < RegisterParameterCount, RegisterParameterIndex += 1) {
		/* For each register parameter */
		
		/* Get the VariableInfo structure for the parameter */
		VariableInfo* NextParameter := (FunctionToCompile->Parameters)[RegisterParameterIndex]
		
		/* And which register it is in (according to the target ABI) */
		i8 NextParameterRegister := 0
		
		if (USE_SYSTEMV) {
			NextParameterRegister := GetSystemVRegister(RegisterParameterIndex)
		}
		else {
			NextParameterRegister := RegisterParameterIndex + 1
			
			if (NextParameterRegister > 2) {
				NextParameterRegister += 5
			}
		}
		
		/* And write the parameter register's value into the parameter's stack space */
		CompileSetVariableValue(cState, NextParameter, NextParameterRegister)
	}
	
	for (i8 StackParameterIndex := 0, StackParameterIndex < StackParameterCount, StackParameterIndex += 1) {
		/* For each stack parameter */
		
		/* Get the VariableInfo structure for the parameter */
		VariableInfo* NextStackParameter := (FunctionToCompile->Parameters)[StackParameterIndex + MAX_REGISTER_PARAMETERS]
		
		/* Load RAX with the parameter's value (using RBX/old RSP as a base) */
		
		i8 NextParameterOffset := (StackParameterIndex + 1) * 8
		i8 NextParameterSize := GetTypeSize(NextStackParameter->Type) As i8
		
		;EmitMove_RX_RIX_DISP8(cState, RAX, RBX, NextParameterOffset, NextParameterSize)
		EmitMove_RX_RIX_DISPX(cState, RAX, RBX, NextParameterOffset, true, NextParameterSize)
		
		/* Write RAX(/the parameter's value) into the parameter's stack space */
		CompileSetVariableValue(cState, NextStackParameter, RAX)
	}
	
	if (cState->pState~>Globals->NextOffset != 0 && (FunctionToCompile->UsesGlobals || IsMain)) {
		EmitMove_R32_I32(cState, R15, cState->GlobalBase)
	}
	
	if (IsMain) {
		ASTNode** GlobalDefaults := cState->pState->GlobalDefaults
		i32 Length := GetArrayLength(GlobalDefaults)
		
		for (i32 GlobalDefaultIndex := 0, GlobalDefaultIndex < Length, GlobalDefaultIndex += 1) {
			ASTNode* NextGlobalDefault := GlobalDefaults[GlobalDefaultIndex]
			VariableInfo* TargetGlobal := NextGlobalDefault->BinaryNode->Left->VariableValue
			
			if (TargetGlobal->IsGlobal != GLOBAL_IS_OPTIMIZED_AWAY) {
				CompileExpression(cState, RAX, NextGlobalDefault)
			}
		}
	}
	
	if (FunctionToCompile->StringBufferSize) {
		i32 StringsEnd := NextLabel(cState)
		
		EmitJump_Label(cState, StringsEnd)
		SetLabel(cState, StringsStart)
		
		cState->CurrentFunctionStringsLabel := StringsStart
		
		for (i32 CharacterIndex := 0, CharacterIndex < FunctionToCompile->StringBufferSize, CharacterIndex += 1) {
			PushByte(cState, FunctionToCompile->StringBuffer[CharacterIndex])
		}
		
		SetLabel(cState, StringsEnd)
	}
	
	/* Compile the body of the function */
	
	Block* Body := FunctionToCompile->Body
	i32 BodyCount := Body->StatementCount
	ASTNode* ReturnExpression := 0
	
	if (BodyCount >= 1) {
		/* If the last statement of the function body is a return, then don't emit a Leave;Ret combo for it 
			since every function automatically gets Leave;Ret emitted for it.
		*/
		
		ASTNode* LastStatement := Body->Statements[BodyCount - 1]
		
		if (LastStatement->NodeType = NODE_TYPE_RETURN) {
			ReturnExpression := LastStatement->ReturnExpression
			Body->StatementCount -= 1
		}
	}
	
	cState->CompilingMain := IsMain
	
	CompileBlock(cState, Body)
	
	if (ReturnExpression) {
		CompileExpression(cState, RAX, ReturnExpression)
		
		i16 ReturnValueSize := GetTypeSize(ReturnExpression->ValueType)
		i16 ReturnTypeSize := GetTypeSize(cState->CurrentFunction->ReturnType)
		
		if (ReturnValueSize < ReturnTypeSize || ReturnValueSize < 4) {
			Cast(cState, RAX, ReturnValueSize As i8)
		}
	}
	
	EmitLeave(cState)
	
	if (IsMain && cState->Config->OutputFormat = OUTPUT_ELF) {
		EmitMove_R64_R64(cState, RDI, RAX)
		EmitMove_R32_I32(cState, RAX, 60)
		EmitSysCall(cState)
	}
	else {
		EmitReturn(cState)
	}
	
	if (cState->RegisterStackIndex != 0) {
		ContextError(FunctionToCompile~>Context, "[INTERNAL] Register stack not empty")
	}
	
	cState->CurrentFunction := 0
}

declare void Compile(CompilerState*, ASTNode*)

define void CompileBlock(CompilerState* cState, Block* BlockToCompile) {
	/* Compiles a block of statements */
	
	for (i32 Index := 0, Index < BlockToCompile->StatementCount, Index += 1) {
		ASTNode* NextStatement := (BlockToCompile->Statements)[Index]
		
		Compile(cState, NextStatement)
	}
}

declare void CompileReturn(CompilerState*, ASTNode*)
declare void CompileIf(CompilerState*, IfGroup*)
declare void CompileFor(CompilerState*, ForLoop*)
declare void CompileLoop(CompilerState*, Block*)
declare void CompileWhile(CompilerState*, WhileLoop*)
declare void CompileTry(CompilerState*, TryCatch*)

declare void CompileUnary(CompilerState*, i8, Type, UnaryExpression*, i8)
declare void CompileBinary(CompilerState*, i8, Type, BinaryExpression*, i8)
declare void CompileGetStructAccess(CompilerState*, i8, StructAccessExpression*)
declare void CompileCall(CompilerState*, i8, ASTNode*)

declare void CompileUnwindTry(CompilerState*, i8)

define void Compile(CompilerState* cState, ASTNode* NodeToCompile) {
	/* Compiles some kind of statement */
	
	i8 NodeType := NodeToCompile->NodeType
	
	if (NodeType = NODE_TYPE_RETURN) {
		CompileReturn(cState, NodeToCompile->ReturnExpression)
	}
	else if (NodeType = NODE_TYPE_IF) {
		CompileIf(cState, NodeToCompile->IfNode)
	}
	else if (NodeType = NODE_TYPE_FOR) {
		CompileFor(cState, NodeToCompile->ForNode)
	}
	else if (NodeType = NODE_TYPE_LOOP) {
		CompileLoop(cState, NodeToCompile->LoopBody)
	}
	else if (NodeType = NODE_TYPE_WHILE) {
		CompileWhile(cState, NodeToCompile->WhileNode)
	}
	else if (NodeType = NODE_TYPE_TRY) {
		CompileTry(cState, NodeToCompile->TryNode)
	}
	else if (NodeType = NODE_TYPE_CONTINUEBREAK) {
		CompileUnwindTry(cState, false)
		
		if (NodeToCompile->IsContinue) {
			EmitJump_Label(cState, cState->CurrentLoopContinueLabel)
		}
		else {
			EmitJump_Label(cState, cState->CurrentLoopBreakLabel)
		}
	}
	else if (NodeType = NODE_TYPE_EXPRESSION) {
		CompileExpression(cState, RAX, NodeToCompile->ExpressionStatement)
	}
	else if (NodeType = NODE_TYPE_ASSEMBLY) {
		CompileAssembly(cState, NodeToCompile->AssemblyValue)
	}
}

define void CompileReturn(CompilerState* cState, ASTNode* ReturnExpression) {
	/* Compile the expression to return */
	
	CompileUnwindTry(cState, true)
	
	CompileExpression(cState, RAX, ReturnExpression)
	
	i16 ReturnValueSize := GetTypeSize(ReturnExpression->ValueType)
	i16 ReturnTypeSize := GetTypeSize(cState->CurrentFunction->ReturnType)
	
	/* And cast it up to (at least) 4 bytes, to ensure no garbage is left in RAX */
	
	if (ReturnValueSize < ReturnTypeSize || ReturnValueSize < 4) {
		Cast(cState, RAX, ReturnValueSize As i8)
	}
	
	EmitLeave(cState)
	
	if (cState->CompilingMain && cState->Config->OutputFormat = OUTPUT_ELF) {
		/* If returning from Main() on Linux, actually call sys_exit() to ensure that the program exits correctly. */
		
		EmitMove_R64_R64(cState, RDI, RAX)
		EmitMove_R32_I32(cState, RAX, 60)
		EmitSysCall(cState)
	}
	else {
		EmitReturn(cState)
	}
}

define i8 IsComparison(Token* OperatorToken) {
	i8 Operator := OperatorToken->Value As i8
	
	return (OPERATOR_FIRST_SHORT_CIRCUIT <= Operator && Operator <= OPERATOR_LAST_COMPARISON)
}

define void CompileCondition(CompilerState* cState, ASTNode* ConditionExpression, i32 JumpToLabel) {
	/* Checks ConditionExpression for being false, and jumps to JumpToLabel if it is 
		This might seem a bit backwards, but loops/if statements all check for false, and jump out (or to the next branch)
		 depending on their conditions being false, which eliminates lots of extra jumping
	*/
	
	i8 ConditionType := ConditionExpression->NodeType
	
	BinaryExpression* BinaryNode := ConditionExpression->BinaryNode
	UnaryExpression* UnaryNode := ConditionExpression->UnaryNode
	
	if (ConditionType = NODE_TYPE_BINARY && IsComparison(BinaryNode->Operator)) {
		/* Compile the expression without a result being stored into RAX, since we use the value in EFLAGS */
		
		i8 Operator := BinaryNode->Operator->Value As i8
		
		ASTNode* Right := BinaryNode->Right
		i8 IsEquals := (Operator = OPERATOR_EQUAL || Operator = OPERATOR_BANG_EQUAL)
		
		if (IsEquals && (Right->NodeType = NODE_TYPE_INTEGER && Right->IntegerValue = 0)) {
			CompileExpression(cState, RAX, BinaryNode->Left)
			
			EmitOr_RX_RX(cState, RAX, RAX, GetTypeSize(BinaryNode->Left->ValueType) As i8)
			
			if (Operator = OPERATOR_EQUAL) {
				EmitJNZ_Label(cState, JumpToLabel)
			}
			else {
				EmitJEZ_Label(cState, JumpToLabel)
			}
		}
		else {
			CompileBinary(cState, RAX, ConditionExpression->ValueType, BinaryNode, true)
			
			/* Use inverted conditions to decide if this branch had a false condition, and to jump past it or not */
			
			if (Operator = OPERATOR_EQUAL) {
				EmitJNZ_Label(cState, JumpToLabel)
			}
			else if (Operator = OPERATOR_BANG_EQUAL) {
				EmitJEZ_Label(cState, JumpToLabel)
			}
			else if (Operator = OPERATOR_LESS) {
				EmitJGE_Label(cState, JumpToLabel)
			}
			else if (Operator = OPERATOR_LESS_EQAUL) {
				EmitJGT_Label(cState, JumpToLabel)
			}
			else if (Operator = OPERATOR_GREATER) {
				EmitJLE_Label(cState, JumpToLabel)
			}
			else if (Operator = OPERATOR_GREATER_EQAUL) {
				EmitJLT_Label(cState, JumpToLabel)
			}
			else if (Operator = OPERATOR_LOGICAL_AND || Operator = OPERATOR_LOGICAL_OR) {
				EmitJEZ_Label(cState, JumpToLabel)
			}
		}
	}
	else if (ConditionType = NODE_TYPE_UNARY && (UnaryNode->Operator->Value As i8) = OPERATOR_BANG) {
		CompileUnary(cState, RAX, ConditionExpression->ValueType, UnaryNode, true)
		EmitJNZ_Label(cState, JumpToLabel)
	}
	else {
		/* In this case, the expression is not conditional, and we will need to check the result for being 0 or not */
		
		CompileExpression(cState, RAX, ConditionExpression)
		EmitTest_RX_RX(cState, RAX, RAX, GetTypeSize(ConditionExpression->ValueType) As i8)
		EmitJEZ_Label(cState, JumpToLabel)
	}
}

define void CompileIf(CompilerState* cState, IfGroup* IfToCompile) {
	i32 BranchCount := IfToCompile->BranchCount
	i32 HasElse := (IfToCompile->ElseBranch) != 0
	
	i32 EndLabel := NextLabel(cState)
	i32 NextBranch := NextLabel(cState)
	
	i8 ConditionResultRegister := PushRegisterStack(cState)
	
	for (i32 BranchIndex := 0, BranchIndex < BranchCount, BranchIndex += 1) {
		SetLabel(cState, NextBranch)
		NextBranch := NextLabel(cState)
		
		CompileCondition(cState, IfToCompile->Conditions[BranchIndex], NextBranch)
		CompileBlock(cState, IfToCompile->Bodies[BranchIndex])
		
		if (HasElse || (BranchIndex != (BranchCount - 1))) {
			/* Only emit a jump to the end label if: We have an 'else', or we are not the last branch 
				This will eliminate a single extra `jmp, endlabel` on the final 'else if' of the statement
				
			*/
			
			EmitJump_Label(cState, EndLabel)
		}
	}
	
	PopRegisterStack(cState)
	
	SetLabel(cState, NextBranch)
	
	if (IfToCompile->ElseBranch) {
		CompileBlock(cState, IfToCompile->ElseBranch)
	}
	
	SetLabel(cState, EndLabel)
}

/* Helpers to save/restore the current loop's continue/break labels (for nested loops) */

bits LoopLables As i64 {
	[0:31] ContinueLabel,
	[32:63] BreakLabel
}
define i64 EnterLoop(CompilerState* cState, i32 ContinueLabel, i32 BreakLabel) {
	LoopLables Result := cState->CurrentLoopContinueLabel
	Result.BreakLabel := cState->CurrentLoopBreakLabel
	
	cState->CurrentLoopContinueLabel := ContinueLabel
	cState->CurrentLoopBreakLabel := BreakLabel
	
	return Result As i64
}
define void ExitLoop(CompilerState* cState, i64 PackedLoopState) {
	LoopLables LoopState := PackedLoopState As LoopLables
	
	cState->CurrentLoopContinueLabel := LoopState.ContinueLabel
	cState->CurrentLoopBreakLabel := LoopState.BreakLabel
}


/* Actual loop compiling functions */

define void CompileFor(CompilerState* cState, ForLoop* ForToCompile) {
	CompileExpression(cState, RAX, ForToCompile->Initializer)
	
	i32 StartLabel := NextLabel(cState)
	i32 ContinueLabel := NextLabel(cState)
	i32 EndLabel := NextLabel(cState)
	
	SetLabel(cState, StartLabel)
	
	CompileCondition(cState, ForToCompile->Condition, EndLabel)
	
	i32 TryDepth := cState->ScopedTryDepth
	cState->ScopedTryDepth := 0
	i64 LoopState := EnterLoop(cState, ContinueLabel, EndLabel)

	/* Prelude stuff done, condition is checked, so now we can compile the body */	
	
	CompileBlock(cState, ForToCompile->Body)
	
	SetLabel(cState, ContinueLabel) /* On continue (or after the body ends naturally) execute the step, and jump to the condition check */
	CompileExpression(cState, RAX, ForToCompile->Step)
	
	EmitJump_Label(cState, StartLabel)
	
	SetLabel(cState, EndLabel) /* Jumped to on ForToCompile->Condition being false, or a break statement */
	
	ExitLoop(cState, LoopState)
	cState->ScopedTryDepth := TryDepth
}

define void CompileLoop(CompilerState* cState, Block* LoopBody) {
	i32 StartLabel := NextLabel(cState)
	i32 EndLabel := NextLabel(cState)
	
	i32 TryDepth := cState->ScopedTryDepth
	cState->ScopedTryDepth := 0
	i64 LoopState := EnterLoop(cState, StartLabel, EndLabel)
	
	SetLabel(cState, StartLabel)
	
	CompileBlock(cState, LoopBody)
	
	EmitJump_Label(cState, StartLabel)
	SetLabel(cState, EndLabel)
	
	ExitLoop(cState, LoopState)
	cState->ScopedTryDepth := TryDepth
}

define void CompileWhile(CompilerState* cState, WhileLoop* WhileToCompile) {
	i32 StartLabel := NextLabel(cState)
	i32 EndLabel := NextLabel(cState)
	
	i32 TryDepth := cState->ScopedTryDepth
	cState->ScopedTryDepth := 0
	i64 LoopState := EnterLoop(cState, StartLabel, EndLabel)
	
	SetLabel(cState, StartLabel)
	
	CompileCondition(cState, WhileToCompile->Condition, EndLabel)
	CompileBlock(cState, WhileToCompile->Body)
	
	EmitJump_Label(cState, StartLabel)
	SetLabel(cState, EndLabel)
	
	ExitLoop(cState, LoopState)
	cState->ScopedTryDepth := TryDepth
}

define void CompileGetStorageValue(CompilerState* cState, ASTNode* ValueToGet) { 
	
	
}
define void CompileSetStorageValue(CompilerState* cState, ASTNode* ValueToSet, i8 RegisterToSetTo) {
	
}

define void CompileGetStorageValuePointer(CompilerState* cState, ASTNode* ValueToGet) {
	
}


define void CompileExpression(CompilerState* cState, i8 ResultRegister, ASTNode* ExpressionToCompile) {
	/* Compiles some kind of expression */
	
	i8 ExpressionType := ExpressionToCompile->NodeType
	Type ResultType := ExpressionToCompile->ValueType
	
	if (ExpressionType = NODE_TYPE_INTEGER) {
		/* Simply move the integer being used into the result register */
		
		i8 ResultSize := GetTypeSize(ResultType)
		i64 IntegerValue := ExpressionToCompile->IntegerValue
		
		if (IntegerValue = 0) {
			EmitXor_RX_RX(cState, ResultRegister, ResultRegister, 4)
		}
		else {
			i8 IntegerSize := GetNumberSize(IntegerValue)
			
			if (ResultSize >= 4 && IntegerSize <= 4) {
				ResultSize := 4
			}
			
			EmitMove_RX_IX(cState, ResultRegister, IntegerValue, ResultSize)
		}
	}
	else if (ExpressionType = NODE_TYPE_ZERO) {
		EmitXor_RX_RX(cState, ResultRegister, ResultRegister, 4)
	}
	else if (ExpressionType = NODE_TYPE_VARIABLE) {
		VariableInfo* TargetVariable := ExpressionToCompile->VariableValue
		
		CompileGetVariableValue(cState, ResultRegister, TargetVariable)
	}
	else if (ExpressionType = NODE_TYPE_UNARY) {
		UnaryExpression* UnaryNode := ExpressionToCompile->UnaryNode
	
		CompileUnary(cState, ResultRegister, ResultType, UnaryNode, false)
	}
	else if (ExpressionType = NODE_TYPE_BINARY) {
		/* Calls into CompileBinary with the correct parameters extracted from ExpressionToCompile */
		
		BinaryExpression* BinaryNode := ExpressionToCompile->BinaryNode
		
		CompileBinary(cState, ResultRegister, ResultType, BinaryNode, false)
	}
	else if (ExpressionType = NODE_TYPE_STRUCT_ACCESS) {
		StructAccessExpression* AccessNode := ExpressionToCompile->AccessNode
		
		CompileGetStructAccess(cState, ResultRegister, AccessNode)
	}
	else if (ExpressionType = NODE_TYPE_CALL) {
		CompileCall(cState, ResultRegister, ExpressionToCompile)
	}
	else if (ExpressionType = NODE_TYPE_STRING) {		
		EmitLea_R64_Label(cState, ResultRegister, cState->CurrentFunctionStringsLabel)
		OffsetLabel(cState, ExpressionToCompile->StringOffsetValue)
	}
	else if (ExpressionType = NODE_TYPE_GET_TYPE_SIZE) {
		i32 OperandTypeSize := GetTypeStorageSize(ExpressionToCompile->TypeValue)
		
		EmitMove_RX_IX(cState, ResultRegister, OperandTypeSize, 4)
	}
}

define void CompileCall(CompilerState* cState, i8 ResultRegister, ASTNode* Node) {
	CallExpression* CallNode := Node->CallNode
	FunctionDefine* TargetFunction := CallNode->TargetFunction
	
	i8 BuiltinID := CallNode->BuiltinID
	i8 IS_SYSCALL := BuiltinID = BUILTIN_SYSCALL /* Used later to decide which set of registers are for parameters */
	
	if (IS_SYSCALL) {
		/* Dummy branch to stop TargetFunction (a null pointer for `syscall(X, ...)`) from being accessed */
	}
	else if (BuiltinID = BUILTIN_BREAKPOINT) {
		EmitInt3(cState)
		
		return 0
	}
	else if (cState->Config->OutputFormat != OUTPUT_PE && TargetFunction->IsImported) {
		ASTError(Node, "Calls to DllImported functions are not allowed when the target output format is not PE.")
	}
	else if !(TargetFunction->IsImported || TargetFunction->HasFullDefinition) {
		ASTError(Node, "Called function is only declared, and never defined")
	}
	
	i32 ParameterCount := CallNode->ParameterCount
	i32 StackParameterCount := 0
	i8 ExtraPadding := false
	i8 HasSavedRAX := false
	
	if (cState->NoClobberA && ResultRegister != RAX) {
		EmitPush_R64(cState, RAX)
		HasSavedRAX := true
		
		cState->NoClobberA := false
	}
	
	/* TargetFunction is only null for syscall:(), so `!TargetFunction` will short circuit before `TargetFunction->IsImported` crashes */
	
	i8 USE_SYSTEMV := !TargetFunction || !TargetFunction->IsImported /* (cState->OutputELF) || (TargetFunction->Attribute = KEYWORD_ASSEMBLY) */
	
	i8 REGISTER_PARAMETER_COUNT := 4
	
	if (USE_SYSTEMV) {
		REGISTER_PARAMETER_COUNT := 6
	}
	
	if (ParameterCount > REGISTER_PARAMETER_COUNT) {
		StackParameterCount := ParameterCount - REGISTER_PARAMETER_COUNT
	}
	
	i8 RegisterStackIndex := cState->RegisterStackIndex
	i8 StackIsAligned := RegisterStackIndex >= 8 && (RegisterStackIndex % 2 != 0)
	
	if (StackParameterCount % 2 || StackIsAligned && !HasSavedRAX) {
		EmitPush_R64(cState, RBX)
		ExtraPadding := true
	}
	
	cState->RegisterStackIndex := 3
	
	i8 OldNoClobberCD89 := cState->NoClobberCD89
	cState->NoClobberCD89 := true /* Ensure none of the parameter registers are used for another operation without being saved */
	
	i32 OldMaxSave := cState->ParameterRegistersMaxSave
	i32 OldMinSave := cState->ParameterRegistersMinSave
	i8 ShouldSaveRegisters := OldMaxSave != OldMinSave
	
	if (ShouldSaveRegisters) {
		for (i32 SaveIndex := OldMinSave, SaveIndex < OldMaxSave, SaveIndex += 1) {
			EmitPush_R64(cState, GetSystemVRegister(SaveIndex))
		}
	}
	
	i32 RegisterParameterCount := ParameterCount - StackParameterCount
	cState->ParameterRegistersMaxSave := RegisterParameterCount
	
	i8 ParameterRegister := RBX
	i8 IS_VARIADIC := (TargetFunction != 0) && (TargetFunction->Attribute = OPERATOR_DOT)
	
	if (ParameterCount != 0) {		
		if (IS_VARIADIC) {
			ParameterCount := TargetFunction->ParameterCount - 1
			
			i8 VariadicCount := ((CallNode->ParameterCount) - (TargetFunction->ParameterCount)) + 1
			i8 VariadicBase := ParameterCount
			
			for (i32 VariadicIndex := (VariadicCount - 1), VariadicIndex >= 0, VariadicIndex -= 1) {
				ASTNode* NextVariadic := (CallNode->Parameters)[VariadicBase + VariadicIndex]
				
				CompileExpression(cState, RBX, NextVariadic)
				
				i16 NextVariadicSize := GetTypeSize(NextVariadic->ValueType)
				
				if (NextVariadicSize <= 4) {
					/* Any parameter which is smaller than 4 bytes will not 0 the upper 32 bits of a register, so we manually cast them up */
					Cast(cState, RBX, NextVariadicSize As i8)
				}
				
				EmitPush_R64(cState, RBX)
			}
			
			EmitMove_R32_I32(cState, RBX, VariadicCount)
			EmitPush_R64(cState, RBX)
		}
		
		for (i64 ParameterIndex := (ParameterCount - 1), ParameterIndex >= 0, ParameterIndex -= 1) {
			i16 ExpectedSize := 8
			
			if !(IS_SYSCALL) {
				VariableInfo* NextParameter := (TargetFunction->Parameters)[ParameterIndex]
				ExpectedSize := GetTypeSize(NextParameter->Type)
			}
			
			ASTNode* NextParameterValue := (CallNode->Parameters)[ParameterIndex]
			
			i8 ThisParameterRegister := ParameterRegister
			
			if (IS_SYSCALL) {
				if (ParameterIndex = 0) {
					ThisParameterRegister := RAX
				}
				else {
					ThisParameterRegister := GetSystemVRegister(ParameterIndex - 1 As i8)
				}
			}
			else if (USE_SYSTEMV) {				
				ThisParameterRegister := GetSystemVRegister(ParameterIndex As i8)
			}
			else {
				if (ParameterIndex = 3) {ThisParameterRegister := R9}
				else if (ParameterIndex = 2) {ThisParameterRegister := R8}
				else if (ParameterIndex = 1) {ThisParameterRegister := RDX}
				else if (ParameterIndex = 0) {ThisParameterRegister := RCX}
			}
			
			cState->ParameterRegistersMinSave := ParameterIndex + 1
			CompileExpression(cState, ThisParameterRegister, NextParameterValue)
			
			i16 ActualSize := GetTypeSize(NextParameterValue->ValueType)
			
			if (NextParameterValue->NodeType = NODE_TYPE_INTEGER) {
				i64 ParameterInteger := NextParameterValue->IntegerValue
				
				if (ActualSize = 4 && ParameterInteger As i32 > 0) {
					ActualSize := ExpectedSize
				}
			}
			
			if (ActualSize < ExpectedSize) {
				Cast(cState, ThisParameterRegister, ActualSize As i8)
			}
			
			if (ParameterIndex >= REGISTER_PARAMETER_COUNT) {
				EmitPush_R64(cState, ThisParameterRegister)
			}
		}
		
		if (IS_VARIADIC) {			
			i8 VariadicRegister := GetSystemVRegister(VariadicBase)
			
			EmitLea_R64_SIB(cState, VariadicRegister, 1, RSP, RSP)
			
			VariadicCount += 1
		}
	}

	cState->NoClobberCD89 := OldNoClobberCD89
	
	if (IS_SYSCALL) {
		EmitSysCall(cState)
	}
	else if (USE_SYSTEMV || !TargetFunction->IsImported) {
		EmitCall_Label(cState, TargetFunction->LabelNumber)
	}
	else {
		EmitSub_R64_I8(cState, RSP, 0x20) /* Shadown space */
		EmitCall_Label(cState, TargetFunction->LabelNumber)
		EmitAdd_R64_I8(cState, RSP, 0x20)
	}
	
	i32 StackAdd := StackParameterCount
	
	if (IS_VARIADIC) {
		StackAdd += VariadicCount
	}
	
	if (StackAdd) {
		EmitAdd_R64_I32(cState, RSP, StackAdd * 8)
	}
	
	if (ShouldSaveRegisters) {
		for (SaveIndex := OldMaxSave, SaveIndex > OldMinSave, SaveIndex -= 1) {
			EmitPop_R64(cState, GetSystemVRegister(SaveIndex - 1))
		}
	}
	
	cState->ParameterRegistersMaxSave := OldMaxSave
	cState->ParameterRegistersMinSave := OldMinSave
	
	if (ExtraPadding) {
		EmitPop_R64(cState, RBX)
	}
	
	if (ResultRegister != RAX) {
		EmitMove_R64_RX(cState, ResultRegister, RAX, GetTypeSize(TargetFunction->ReturnType) As i8)
	}
	
	if (HasSavedRAX) {
		EmitPop_R64(cState, RAX)
		cState->NoClobberA := true
	}
	
	cState->RegisterStackIndex := RegisterStackIndex
}

declare void CompileGetStructAccessPointer(CompilerState*, i8, StructAccessExpression*)

define void CompileStructAccessLeft(CompilerState* cState, i8 ResultRegister, StructAccessExpression* TargetAccess) {
	ASTNode* Left := TargetAccess->Left
	
	if (TargetAccess->Operator->Value = OPERATOR_DOT) {
		if (Left->NodeType = NODE_TYPE_STRUCT_ACCESS) {
			return CompileGetStructAccessPointer(cState, ResultRegister, Left->AccessNode)
		}
		else if (Left->NodeType = NODE_TYPE_VARIABLE) {
			return CompileGetVariableAddress(cState, ResultRegister, Left->VariableValue)
		}
	}
	
	CompileExpression(cState, ResultRegister, Left)
}

define void CompileGetStructAccessPointer(CompilerState* cState, i8 ResultRegister, StructAccessExpression* TargetAccess) {
	StructField* TargetField := TargetAccess->TargetField
	i16 TargetFieldSize := GetTypeSize(TargetField->ValueType)
	
	CompileStructAccessLeft(cState, ResultRegister, TargetAccess)
	
	i32 FieldOffset := TargetField->Offset
	
	if (FieldOffset != 0) {
		EmitLea_R64_R64_DISPX(cState, ResultRegister, ResultRegister, FieldOffset)
	}
}

define void CompileGetStructAccess(CompilerState* cState, i8 ResultRegister, StructAccessExpression* TargetAccess) {
	if (TargetAccess->Operator->Value = OPERATOR_TILDE_GREATER) {
		return CompileGetStructAccessPointer(cState, ResultRegister, TargetAccess)
	}
	
	StructField* TargetField := TargetAccess->TargetField
	i8 TargetFieldSize := GetTypeSize(TargetField->ValueType) As i8
	
	i32 FieldOffset := TargetField->Offset
	
	if (TargetField->BitStartIndex || TargetField->BitEndIndex) {
		CompileExpression(cState, ResultRegister, TargetAccess->Left)
		
		TargetFieldSize := FieldOffset
		
		i32 FieldSize := (TargetFieldSize * 8) - 1
		i8 ShiftLeftBy := FieldSize - TargetField->BitEndIndex
		i8 ShiftRightBy := ShiftLeftBy + TargetField->BitStartIndex
		
		if (ShiftLeftBy < 0) {
			ShiftRightBy := -ShiftLeftBy
		}
		else if (ShiftLeftBy) { 
			EmitSAL_RX_I8(cState, ResultRegister, ShiftLeftBy, TargetFieldSize)
		}
		
		if (ShiftRightBy) {
			EmitSAR_RX_I8(cState, ResultRegister, ShiftRightBy, TargetFieldSize)
		}
	}
	else {
		CompileStructAccessLeft(cState, ResultRegister, TargetAccess)
		
		if (FieldOffset = 0 && ResultRegister != R13) {
			EmitMove_RX_RIX(cState, ResultRegister, ResultRegister, TargetFieldSize)
		}
		;else if (FieldOffset >= 112) {
		;	EmitMove_RX_RIX_DISP32(cState, ResultRegister, ResultRegister, FieldOffset, TargetFieldSize)
		;}
		else {
			;EmitMove_RX_RIX_DISP8(cState, ResultRegister, ResultRegister, FieldOffset As i8, TargetFieldSize)
			EmitMove_RX_RIX_DISPX(cState, ResultRegister, ResultRegister, FieldOffset, !(FieldOffset >= 112), TargetFieldSize) 
		}
	}
}

declare void CompileSetArrayAccess(CompilerState*, i8, ASTNode*, ASTNode*, i8)

define void CompileSetStructAccess(CompilerState* cState, i8 ValueRegister, StructAccessExpression* TargetAccess) {
	StructField* TargetField := TargetAccess->TargetField
	
	i8 ResultPointerRegister := PushRegisterStack(cState)
	
	i32 FieldOffset := TargetField->Offset
	i8 TargetFieldSize := GetTypeSize(TargetField->ValueType)
	
	if (TargetField->BitStartIndex || TargetField->BitEndIndex) {
		CompileExpression(cState, ResultPointerRegister, TargetAccess->Left)
		
		TargetFieldSize := TargetField->Offset
		
		i8 StartIndex := TargetField->BitStartIndex
		i8 EndIndex := TargetField->BitEndIndex
		
		i32 FieldSize := (TargetFieldSize * 8) - 1
		i8 ShiftLeftBy := FieldSize - (EndIndex - StartIndex)
		i8 ShiftRightBy := FieldSize - EndIndex
		
		i64 PartialMask := ((1 As i64) << ((EndIndex + 1) As i8)) - 1
		
		if (EndIndex = 63) {
			/* Since 1<<64 is undefined, the result of INT64_MAX needs to be hard coded */
			PartialMask := 0xFFFFFFFFFFFFFFFF
		}
		
		i64 Mask := ~(PartialMask >> StartIndex << StartIndex)
		
		EmitMove_RX_IX(cState, RDI, Mask, TargetFieldSize)
		EmitAnd_RX_RX(cState, ResultPointerRegister, RDI, TargetFieldSize)
		
		if (ShiftLeftBy) { 
			EmitSAL_RX_I8(cState, ValueRegister, ShiftLeftBy, TargetFieldSize)
		}
		
		EmitSAR_RX_I8(cState, ValueRegister, ShiftRightBy, TargetFieldSize)
		EmitOr_RX_RX(cState, ResultPointerRegister, ValueRegister, TargetFieldSize)
		
		ASTNode* Left := TargetAccess->Left
		i8 LeftNodeType := Left->NodeType
		
		if (LeftNodeType = NODE_TYPE_VARIABLE) {
			CompileSetVariableValue(cState, Left->VariableValue, ResultPointerRegister)
		}
		else if (LeftNodeType = NODE_TYPE_STRUCT_ACCESS) {
			CompileSetStructAccess(cState, ResultPointerRegister, Left->AccessNode)
		}
		else if (LeftNodeType = NODE_TYPE_BINARY) {
			CompileSetArrayAccess(cState, ValueRegister, Left->BinaryNode->Left, Left->BinaryNode->Right, ResultPointerRegister)
		}
	}
	else {
		CompileStructAccessLeft(cState, ResultPointerRegister, TargetAccess)
		
		i8 TargetSize := GetTypeSize(TargetField->ValueType) As i8
		
		if (FieldOffset = 0 && ResultPointerRegister != R13) {
			EmitMove_RIX_RX(cState, ResultPointerRegister, ValueRegister, TargetSize)
		}
		else if (FieldOffset >= 112) {
			EmitMove_RIX_DISP32_RX(cState, ResultPointerRegister, ValueRegister, FieldOffset, TargetSize)
		}
		else {
			EmitMove_RIX_DISP8_RX(cState, ResultPointerRegister, ValueRegister, FieldOffset As i8, TargetSize)
		}
	}
	
	PopRegisterStack(cState)
}

define void CompileArrayAccess(CompilerState* cState, i8 ResultRegister, ASTNode* Pointer, ASTNode* Index, i8 ValueRegister, i8 GetAddress) {
	i8 PointerRegister := ResultRegister
	CompileExpression(cState, PointerRegister, Pointer)
	
	i8 IndexRegister := PushRegisterStack(cState)
	CompileExpression(cState, IndexRegister, Index)
	
	i8 IndexSize := GetTypeSize(Index->ValueType) As i8
	
	if (IndexSize < 4) {
		Cast(cState, IndexRegister, IndexSize)
	}
	
	Type PointerType := Pointer->ValueType
	
	i8 IndexScale := 8
	
	TypeInfo* PointerBase := GetBaseType(PointerType)
	i16 PointerDepth := GetPointerDepth(PointerType)
	
	if (PointerDepth = 1) {
		IndexScale := (PointerBase->Size) As i8
	}
	
	i8 IsSet := !(ValueRegister < 0)
	
	if (IsSet || GetAddress) {
		EmitLea_R64_SIB(cState, ResultRegister, IndexScale, IndexRegister, PointerRegister)
		
		if (IsSet) {
			EmitMove_RIX_RX(cState, ResultRegister, ValueRegister, IndexScale)
		}
	}
	else {
		EmitMove_RX_SIB(cState, ResultRegister, IndexScale, IndexRegister, PointerRegister)
	}
	
	PopRegisterStack(cState)
}

define void CompileGetArrayAccess(CompilerState* cState, i8 ResultRegister, ASTNode* Pointer, ASTNode* Index, i8 GetAddress) {
	CompileArrayAccess(cState, ResultRegister, Pointer, Index, -1 As i8, GetAddress)
}
define void CompileSetArrayAccess(CompilerState* cState, i8 ResultRegister, ASTNode* Pointer, ASTNode* Index, i8 ValueRegister) {
	CompileArrayAccess(cState, ResultRegister, Pointer, Index, ValueRegister, false)
}

define void CompileUnary(CompilerState* cState, i8 ResultRegister, Type ResultType, UnaryExpression* ExpressionToCompile, i8 DiscardResult) {
	/* Compiles a unary operator and operand */
	
	i8 Operator := (ExpressionToCompile->Operator->Value) As i8
	i8 ResultSize := GetTypeSize(ResultType) As i8
	
	if (Operator = OPERATOR_AND) {
		ASTNode* Operand := ExpressionToCompile->Operand
		i8 OperandType := Operand->NodeType
		
		if (OperandType = NODE_TYPE_VARIABLE) {
			VariableInfo* OperandVariable := Operand->VariableValue
			
			CompileGetVariableAddress(cState, ResultRegister, OperandVariable)
		}
		else if (OperandType = NODE_TYPE_STRUCT_ACCESS) {
			StructAccessExpression* OperandStructAccess := Operand->AccessNode
			
			CompileGetStructAccessPointer(cState, ResultRegister, OperandStructAccess)
		}
		else if (OperandType = NODE_TYPE_BINARY) {
			BinaryExpression* OperandExpression := Operand->BinaryNode
			
			if (OperandExpression->Operator->Value = OPERATOR_OPEN_BRACKET) {
				CompileGetArrayAccess(cState, ResultRegister, OperandExpression->Left, OperandExpression->Right, true)
			}
		}
		else if (OperandType = NODE_TYPE_DEFINE) {
			FunctionDefine* OperandFunction := Operand->DefineNode
			
			EmitLea_RAX_Label(cState, OperandFunction->LabelNumber)
			EmitMove_R64_R64(cState, ResultRegister, RAX)
		}
	}
	else {
		/* Only compile the operand when the operator isn't `&` (since it doesn't use the operand value) */
		CompileExpression(cState, ResultRegister, ExpressionToCompile->Operand)
		
		if (Operator = OPERATOR_STAR) {
			EmitMove_RX_RIX(cState, ResultRegister, ResultRegister, ResultSize)
		}
		else if (Operator = OPERATOR_MINUS) {
			EmitNeg_RX(cState, ResultRegister, ResultSize)
		}
		else if (Operator = OPERATOR_TILDE) {
			EmitNot_RX(cState, ResultRegister, ResultSize)
		}
		else if (Operator = OPERATOR_BANG) {
			EmitTest_RX_RX(cState, ResultRegister, ResultRegister, GetTypeSize(ExpressionToCompile->Operand->ValueType) As i8)
			
			if !(DiscardResult) {
				/* If the caller uses the FLAGS register, it doesn't need a 'true' result, and can just use what's in the FLAGS register */
				EmitSetE_R8(cState, ResultRegister)
			}
		}
	}
}

define void CompileBinaryOperator(CompilerState* cState, i8 Operator, i8 ResultSize, i8 LeftRegister, i8 RightRegister) {
	i8 ResultRegister := LeftRegister
	
	if (Operator = OPERATOR_PLUS) {
		EmitAdd_RX_RX(cState, LeftRegister, RightRegister, ResultSize)
	}
	else if (Operator = OPERATOR_MINUS) {
		EmitSub_RX_RX(cState, LeftRegister, RightRegister, ResultSize)
	}
	else if (Operator = OPERATOR_STAR) {
		if (ResultSize = 1) {
			/* Do 8 bit multiplication as 16 bit, and cast the result back down to 8 bits (since there is no imul r8, r8 instruction */
			EmitIMul_RX_RX(cState, LeftRegister, RightRegister, 2)
			Cast(cState, LeftRegister, 1)
		}
		else {	
			EmitIMul_RX_RX(cState, LeftRegister, RightRegister, ResultSize)
		}
	}
	else if (Operator = OPERATOR_SLASH || Operator = OPERATOR_PERCENT) {
		if (LeftRegister != RAX) {
			EmitMove_R64_R64(cState, RBX, RAX)
			EmitMove_R64_R64(cState, RAX, LeftRegister)
		}
		if (cState->NoClobberCD89) {
			EmitPush_R64(cState, RDX)
		}
		
		if (ResultSize = 1) {
			/* Handle 8 bit division through a 16 bit operation, which will have a result casted down 
				(8 bit IDIV uses AL/AH, which CodeGen isn't set up to encode)
			*/
			EmitCXX(cState, 2)
			EmitIDiv_RAX_RX(cState, RightRegister, 2)
		}
		else {
			EmitCXX(cState, ResultSize)
			EmitIDiv_RAX_RX(cState, RightRegister, ResultSize)
		}
		
		if (Operator = OPERATOR_SLASH && ResultRegister != RAX) {
			EmitMove_RX_RX(cState, ResultRegister, RAX, ResultSize)
		}
		else if (Operator = OPERATOR_PERCENT && ResultRegister != RDX) {
			EmitMove_RX_RX(cState, ResultRegister, RDX, ResultSize)
		}
		
		if (cState->NoClobberCD89) {
			EmitPop_R64(cState, RDX)
		}
		if (LeftRegister != RAX) {
			EmitMove_R64_R64(cState, RAX, RBX)
		}
		
		if (ResultSize = 1) {
			Cast(cState, ResultRegister, 1)
		}
	}
	else if (Operator = OPERATOR_EQUAL) {
		EmitSetE_R8(cState, ResultRegister)
	}
	else if (Operator = OPERATOR_BANG_EQUAL) {
		EmitSetNE_R8(cState, ResultRegister)
	}
	else if (Operator = OPERATOR_LESS) {
		EmitSetL_R8(cState, ResultRegister)
	}
	else if (Operator = OPERATOR_LESS_EQAUL) {
		EmitSetLE_R8(cState, ResultRegister)
	}
	else if (Operator = OPERATOR_GREATER) {
		EmitSetG_R8(cState, ResultRegister)
	}
	else if (Operator = OPERATOR_GREATER_EQAUL) {
		EmitSetGE_R8(cState, ResultRegister)
	}
	else if (Operator = OPERATOR_XOR) {
		EmitXor_RX_RX(cState, LeftRegister, RightRegister, ResultSize)
	}
	else if (Operator = OPERATOR_AND) {
		EmitAnd_RX_RX(cState, LeftRegister, RightRegister, ResultSize)
	}
	else if (Operator = OPERATOR_OR) {
		EmitOr_RX_RX(cState, LeftRegister, RightRegister, ResultSize)
	}
}

define i8 IsPowerOfTwo(i64 Number) {
	return Number && !(Number & (Number - 1))
}
define i8 FindFirstBitSet(i64 Number) asm {
	bsr, rax, rdi
	ret
}

define void CompileBinary(CompilerState* cState, i8 ResultRegister, Type ResultType, BinaryExpression* ExpressionToCompile, i8 DiscardResult) {
	/* Compiles a binary expression, and both operands of the expression */
	
	ASTNode* Left := ExpressionToCompile->Left
	ASTNode* Right := ExpressionToCompile->Right
	
	i8 LeftSize := GetTypeSize(Left->ValueType) As i8
	i8 RightSize := GetTypeSize(Right->ValueType) As i8
	i8 ResultSize := GetTypeSize(ResultType) As i8
	
	i8 LeftRegister := ResultRegister
	i8 RightRegister := 0
	
	i8 Operator := (ExpressionToCompile->Operator->Value) As i8
	
	if (OPERATOR_FIRST_ASSIGNMENT <= Operator && Operator <= OPERATOR_LAST_ASSIGNMENT) {
		/* This is an assignment, so we can reuse the result register for the side side, and ignore the left */
		
		i8 LeftType := (Left->NodeType) As i8
		
		VariableInfo* TargetVariable := Left->VariableValue
		StructAccessExpression* LeftAccess := Left->AccessNode
		BinaryExpression* LeftExpression := Left->BinaryNode
		
		i8 WorkRegister := PushRegisterStack(cState)
		
		if (Operator = OPERATOR_COLON_EQUAL) {
			CompileExpression(cState, ResultRegister, Right)
			
			if (RightSize < LeftSize) {
				Cast(cState, ResultRegister, RightSize)
			}
			
			if (LeftType = NODE_TYPE_VARIABLE) {
				CompileSetVariableValue(cState, TargetVariable, ResultRegister)
			}
			else if (LeftType = NODE_TYPE_STRUCT_ACCESS) {
				CompileSetStructAccess(cState, ResultRegister, LeftAccess)
			}
			else if (LeftType = NODE_TYPE_BINARY) {
				CompileSetArrayAccess(cState, RBX, LeftExpression->Left, LeftExpression->Right, ResultRegister)
			}
		}
		else {				
			CompileExpression(cState, WorkRegister, Right)
			
			if (RightSize < LeftSize) {
				Cast(cState, WorkRegister, RightSize)
			}
			
			/* Get old value of left side */
			
			if (LeftType = NODE_TYPE_VARIABLE) {
				i32 Start := cState->Index
				i32 Offset := GetVariableOffset(cState, TargetVariable, null)
				
				if (Operator = OPERATOR_PLUS_EQUAL) {
					EmitAdd_RIX_DISPX_RX(cState, RBP, WorkRegister, Offset, ResultSize)
				}
				else if (Operator = OPERATOR_MINUS_EQUAL) {
					EmitSub_RIX_DISPX_RX(cState, RBP, WorkRegister, Offset, ResultSize)
				}
				
				if (cState->Index != Start) {
					PopRegisterStack(cState)
					return 0
				}
				
				CompileGetVariableValue(cState, ResultRegister, TargetVariable)
			}
			else if (LeftType = NODE_TYPE_STRUCT_ACCESS) {
				CompileGetStructAccess(cState, ResultRegister, LeftAccess)
			}
			else if (LeftType = NODE_TYPE_BINARY) {
				CompileGetArrayAccess(cState, ResultRegister, LeftExpression->Left, LeftExpression->Right, false)
			}
			
			/* Translate X= operators into just X operators, so CompileBinaryOperator does the correct operation */
			if (Operator <= OPERATOR_PERCENT_EQUAL) {	
				Operator += OPERATOR_PLUS - OPERATOR_PLUS_EQUAL
			}
			else {
				Operator += OPERATOR_AND - OPERATOR_AND_EQUAL
			}
			
			/* Do the actual operation */
			CompileBinaryOperator(cState, Operator, ResultSize, ResultRegister, WorkRegister)
			
			/* Store the new value for each valid left side */
			
			if (LeftType = NODE_TYPE_VARIABLE) {
				CompileSetVariableValue(cState, TargetVariable, ResultRegister)
			}
			else if (LeftType = NODE_TYPE_STRUCT_ACCESS) {
				CompileSetStructAccess(cState, ResultRegister, LeftAccess)
			}
			else if (LeftType = NODE_TYPE_BINARY) {
				CompileSetArrayAccess(cState, WorkRegister, LeftExpression->Left, LeftExpression->Right, ResultRegister)
			}
		}
		
		PopRegisterStack(cState)
	}
	else if (Operator = OPERATOR_OPEN_BRACKET) {
		CompileGetArrayAccess(cState, ResultRegister, Left, Right, false)
	}
	else {
		/* As long as we're not compiling an assignment, compile the left operand into the result register */
		
		CompileExpression(cState, LeftRegister, Left)
		
		i8 OldClobberA := cState->NoClobberA
		
		if (LeftRegister = RAX) {
			cState->NoClobberA := true
		}
		
		if (Operator = OPERATOR_AS) {
			/* For 'As', just compile the left operand, cast it, and return */
			Type RightType := Right->TypeValue
			
			if (Left->NodeType != NODE_TYPE_INTEGER) {
				/* Only emit cast instructions for values which aren't integers, since integer casts are done at compile time */
				
				Cast(cState, LeftRegister, GetTypeSize(RightType) As i8)
			}
		}
		else if (OPERATOR_FIRST_SHORT_CIRCUIT <= Operator && Operator <= OPERATOR_LAST_SHORT_CIRCUIT) {
			if (NodeHasSideEffects(Right)) {
				i32 End := NextLabel(cState)
				
				EmitTest_RX_RX(cState, LeftRegister, LeftRegister, LeftSize)
				
				if !(DiscardResult) {
					EmitSetNE_R8(cState, ResultRegister)
				}
				
				if (Operator = OPERATOR_LOGICAL_AND) {
					EmitJEZ_Label(cState, End)
				}
				else if (Operator = OPERATOR_LOGICAL_OR) {
					EmitJNZ_Label(cState, End)
				}
				
				CompileExpression(cState, LeftRegister, Right)
				
				EmitTest_RX_RX(cState, LeftRegister, LeftRegister, RightSize)
				
				if !(DiscardResult) {
					EmitSetNE_R8(cState, ResultRegister)
				}
				
				SetLabel(cState, End)
			}
			else {
				RightRegister := PushRegisterStack(cState)
				CompileExpression(cState, RightRegister, Right)
				
				i8 MaxOperandSize := LeftSize
				
				if (RightSize > LeftSize) {
					MaxOperandSize := RightSize
					Cast(cState, LeftRegister, LeftSize)
				}
				else if (RightSize < LeftSize) {
					Cast(cState, RightRegister, RightSize)
				}
				
				if (Operator = OPERATOR_LOGICAL_AND) {
					EmitIMul_RX_RX(cState, LeftRegister, RightRegister, MaxOperandSize)
				}
				else if (Operator = OPERATOR_LOGICAL_OR) {
					EmitAdd_RX_RX(cState, LeftRegister, RightRegister, MaxOperandSize)
				}
				
				EmitOr_RX_RX(cState, LeftRegister, LeftRegister, MaxOperandSize)
				
				if !(DiscardResult) {
					EmitSetNE_R8(cState, ResultRegister)
				}
				
				PopRegisterStack(cState)
				
				return 0
			}
		}
		else if (Operator = OPERATOR_LESS_LESS || Operator = OPERATOR_GREATER_GREATER) {
			if (Right->NodeType = NODE_TYPE_INTEGER) {
				if (Operator = OPERATOR_LESS_LESS) {
					EmitSAL_RX_I8(cState, LeftRegister, Right->IntegerValue As i8, ResultSize)
				}
				else {
					EmitSAR_RX_I8(cState, LeftRegister, Right->IntegerValue As i8, ResultSize)
				}
			}
			else {
				i8 LeftWasRCX := LeftRegister = RCX
				
				if (LeftWasRCX) {
					LeftRegister := PushRegisterStack(cState)
					EmitMove_RX_RX(cState, LeftRegister, RCX, ResultSize)
				}
				else if (cState->NoClobberCD89) {
					EmitPush_R64(cState, RCX)
				}
				
				CompileExpression(cState, RCX, Right)
				
				if (Operator = OPERATOR_LESS_LESS) {
					EmitSHL_RX_CL(cState, LeftRegister, ResultSize)
				}
				else if (Operator = OPERATOR_GREATER_GREATER) {
					EmitSHR_RX_CL(cState, LeftRegister, ResultSize)
				}
				
				if (LeftWasRCX) {
					EmitMove_RX_RX(cState, RCX, LeftRegister, ResultSize)
					PopRegisterStack(cState)
				}
				else if (cState->NoClobberCD89) {
					EmitPop_R64(cState, RCX)
				}
			}
		}
		else {
			i8 IsComparison := IsComparison(ExpressionToCompile->Operator)
			
			if (IsComparison) {
				i8* ErrorMessage := 0
				
				Type TrueResultType := TryGetBinaryResultType(cState->pState, Left->ValueType, Right->ValueType, &ErrorMessage)
				
				if !(ErrorMessage) {
					ResultSize := GetTypeSize(TrueResultType)
				}
				else {
					ResultSize := 8
				}
			}
			
			/* If either operand is smaller than the result type, cast it up to the R64 (aka any possible size) */
			if (LeftSize < ResultSize) {
				Cast(cState, LeftRegister, LeftSize)
			}
			
			i8 IsDivision := (Operator = OPERATOR_SLASH || Operator = OPERATOR_PERCENT)
			
			if (IsDivision && Right->NodeType = NODE_TYPE_INTEGER && IsPowerOfTwo(Right->IntegerValue)) {
				if (Operator = OPERATOR_SLASH) {
					EmitSAR_RX_I8(cState, LeftRegister, FindFirstBitSet(Right->IntegerValue), LeftSize)
				}
				else {
					i64 Mask := Right->IntegerValue - 1
					
					if (LeftSize <= 4) {
						EmitAnd_RX_IX(cState, LeftRegister, Mask As i32, LeftSize)
					}
					else {
						EmitMove_R64_I64(cState, RBX, Mask)
						EmitAnd_RX_RX(cState, LeftRegister, RBX, 8)
					}
				}
				
				return 0
			}
			
			i8 IsPlusMinus := (Operator = OPERATOR_PLUS || Operator = OPERATOR_MINUS)
			
			if (IsPlusMinus && Right->NodeType = NODE_TYPE_INTEGER) {
				/* Since some instructions for simple operations have encodings for sign-extended immediates 
					We can check for an operator+operand combo which can be optimized into a single instruction
				*/
				
				i64 RightInteger := Right->IntegerValue
				
				i8 OperandSize := GetNumberSize(RightInteger)
				
				if (OperandSize <= 4) {
					/* There are no imm64 encodings, only imm8 and imm32 */
					
					if (Operator = OPERATOR_PLUS) {
						EmitAdd_R64_IX(cState, ResultRegister, RightInteger As i32)
					}
					else if (Operator = OPERATOR_MINUS) {
						EmitSub_R64_IX(cState, ResultRegister, RightInteger As i32)
					}
					
					/* When we optimized this expression, we need to return early to skip it being
						compiled like normal
					*/
					
					cState->NoClobberA := OldClobberA
					return 0
				}
			}
			
			/* Allocate a new register for the right operand */
			
			RightRegister := PushRegisterStack(cState)
			CompileExpression(cState, RightRegister, Right)
			
			if (RightSize < ResultSize) {
				Cast(cState, RightRegister, RightSize)
			}
			
			if (IsComparison) {
				EmitCmp_RX_RX(cState, LeftRegister, RightRegister, ResultSize)
			}
			
			/* Also uses uses the correct instruction for two ResultType sized operands (casting should be done this is called) */
			
			if (DiscardResult) {
				/* Since comparison expressions can technically store a result in the FLAGS register, sometimes
					 a comparison can have the "real" result discarded, and the caller can just use FLAGS.
					So, this is a dummy branch to prevent code from being generated to grab the result of an
					 expression out of FLAGS.
				*/
			}
			else {
				CompileBinaryOperator(cState, Operator, ResultSize, LeftRegister, RightRegister)
			}
			
			PopRegisterStack(cState) /* Pop the right register */
		}
		
		cState->NoClobberA := OldClobberA
	}
}

define void CompileUnwindTry(CompilerState* cState, i8 ForReturn) {
	; Ensures that the current function's stack frame is restored
	; 	For `return`, this means all `Try` stack frame modifications 
	; 	For `continue`/`break`, this means only `Try` stack frame modifications inside of the current loop
	
	i32 Total := cState->ScopedTryDepth
	
	if (ForReturn) {
		Total := cState->TotalTryDepth
	}
	
	for (i32 Index := 0, Index < Total, Index += 1) {
		EmitPop_R64_DISP8(cState, RBP, 0)	; Restore saved stack frame
		EmitPop_R64_DISP8(cState, RBP, 8)
	}
}

define void CompileTry(CompilerState* cState, TryCatch* TryToCompile) {
	i32 AfterCatchLabel := NextLabel(cState)
	i32 TryEndLabel := NextLabel(cState)
	
	; try {
	EmitPush_R64_DISP8(cState, RBP, 8) ; Save the old stack frame
	EmitPush_R64_DISP8(cState, RBP, 0)
	
	EmitMove_R64_R64(cState, RBX, RSP)		; Save (RSP | Magic) into stack frame
	EmitAdd_R64_I8(cState, RBX, 1)
	EmitMove_RIX_DISP8_RX(cState, RBP, RBX, 0, 8)
	
	EmitLea_R64_Label(cState, RBX, TryEndLabel)		; Save (CatchLocation) into stack frame
	EmitMove_RIX_DISP8_RX(cState, RBP, RBX, 8, 8)
	
	cState->TotalTryDepth += 1
	cState->ScopedTryDepth += 1
	CompileBlock(cState, TryToCompile->Body)
	cState->ScopedTryDepth -= 1
	cState->TotalTryDepth -= 1
	
	EmitPop_R64_DISP8(cState, RBP, 0)	; Restore saved stack frame
	EmitPop_R64_DISP8(cState, RBP, 8)
	; }
	
	if (TryToCompile->CatchBody) {
		EmitJump_Label(cState, AfterCatchLabel)
	}
	
	; catch {
	SetLabel(cState, TryEndLabel)
	
	if (TryToCompile->CatchBody) {
		EmitPop_R64_DISP8(cState, RBP, 0)	; Restore saved stack frame
		EmitPop_R64_DISP8(cState, RBP, 8)
		
		if (TryToCompile->CatchCodeVariable) {
			CompileSetVariableValue(cState, TryToCompile->CatchCodeVariable, RDI)
		}
		
		CompileBlock(cState, TryToCompile->CatchBody)
		
		SetLabel(cState, AfterCatchLabel)
	}
	; }
}

#Include ./src/compiler/AssemblyCompile.rlx
