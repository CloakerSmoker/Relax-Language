define void RestoreHandlerContextAndTransferControl(i64 Code, void* Handler, void* OldRBP, void* OldRSP) asm {
	; Restores the execution context to how the function containing the handler expects, and then
	;  calls the handler passing the exception code
	
	mov, rbp, rdx
	mov, rsp, rcx
	jmp, rsi
}

struct StackFrame {
	StackFrame* LastFrame,
	i64* ReturnAddress
}

define i64* GetRBP() asm {
	mov, rax, rbp
	ret
}

/* 
	`Try` will (With `ThisFrame := RBP As StackFrame*`):
		1) Push `ThisFrame->LastFrame`		(saving it)
		2) Push `ThisFrame->ReturnAddress`	(saving it)
		
		3) Set `ThisFrame->LastFrame := RSP | 1`
		4) Set `ThisFrame->ReturnAddress := &CatchBlock`
		
	And `Throw` will:
		1) Walk up `ThisFrame->LastFrame` as a singly linked list, looking for `ThisFrame->LastFrame & 1`
			When `ThisFrame->LastFrame & 1` is true, we know that we've found a frame altered by `Try`
			which will have set `ThisFrame->ReturnAddress` to the address of a `Catch` block
		2) Set RBP to `ThisFrame`, restorting the base pointer for the function which contains 
			the `Catch` block, restoring access to any local variables of the function
		3) Set RSP to `ThisFrame->LastFrame - 1` which is equal to RSP inside of the `Try` block of the function
		4) Jump to `ThisFrame->ReturnAddress`, transferring control to the `catch` block, passing the exception
			code in RDI
	
	The end of a `Try` (or the start of a `Catch`) will:
		1) Pop the saved version of `ThisFrame->ReturnAddress` back into `ThisFrame->ReturnAddress`
		2) Pop the saved version of `ThisFrame->LastFrame` back into `ThisFrame->LastFrame`
		3) (If the catch block exists, and has a code variable) set the code variable to RDI passed by `Throw` 
	
	This *should* work in most cases, since `ThisFrame->LastFrame` (on all platforms) must be aligned to at least a 
		multiple of 8, and shockingly enough, both 1 and 9 are not multiples of 8, so there will never be a valid
		stack frame with `ThisFrame->LastFrame & 1` set (unless it is an exception handler).
	
	One potential problem is with outside software trying to walk the stack, but from what I've seen, all debuggers
		couldn't figure out the braindead simple stack layout *before* exception handling was added, so it doesn't
		really matter.
	
	Another problem is if a function `return`s while the stack frame is altered by one or more `try` blocks, which
		would cause RBP to be set to a corrupted RSP, and the `ret` portion of the return would jump to garbage
		memory (assuming the stack misalignment doesn't instantly crash everything)
	
	Really, the last problem applies to a `Try` stack frame modification ever being leaked, but without a funky
		inline assembly jump which never runs the tail end of the `Try` statement, this shouldn't be possible.
		
		
	Todo: Translate system errors/signals into native exceptions
	
*/

define StackFrame* FindHandler(i64* StartingFrame) {
	; Walks up stack frames looking for one marked as an exception handler
	
	StackFrame* Last := StartingFrame As StackFrame*
	
	while (Last) {
		if (Last->LastFrame & 1) {
			return Last
		}
		
		Last := Last->LastFrame
	}
	
	return null
}

void* UnhandledExceptionHandler := null

define void CallUnhandledExceptionFilter(i64 Code, void* Handler) asm {
	jmp, rsi
}

DllImport i64 GetCurrentProcess() {Kernel32.dll, GetCurrentProcess}
DllImport void TerminateProcess(i64, i64) {Kernel32.dll, TerminateProcess}

i8 EXCEPTION_WAS_LAST_CHANCE := false

define void CallUnhandledExceptionHandler(i64 Code) {
	if (UnhandledExceptionHandler != null && !EXCEPTION_WAS_LAST_CHANCE) {
		EXCEPTION_WAS_LAST_CHANCE := true
		
		CallUnhandledExceptionFilter(Code, UnhandledExceptionHandler)
	}
	
	windows {
		TerminateProcess(GetCurrentProcess(), Code)
	}
	linux {
		syscall:(60, Code)
	}
}

define void Throw(i64 Code) {
	; Calls the first exception handler found in the call stack
	
	StackFrame* Last := FindHandler(GetRBP())
	
	if (Last = null) {
		CallUnhandledExceptionHandler(Code)
	}
	else {
		RestoreHandlerContextAndTransferControl(Code, Last->ReturnAddress, Last, Last->LastFrame - 1)
	}
}


struct WindowsExceptionRecord {
	i32 ExceptionCode,
	i32 ExceptionFlag,
	WindowsExceptionRecord* ExceptionRecord,
	void* ExceptionAddress,
	i32 ExceptionParameters
}

struct WindowsContextRecord {
    i64 P1Home,
    i64 P2Home,
    i64 P3Home,
    i64 P4Home,
    i64 P5Home,
    i64 P6Home,

    i32 ContextFlags,
    i32 MxCsr,

    i16 CS,
    i16 DS,
    i16 ES,
    i16 FS,
    i16 GS,
    i16 SS,
    i32 EFlags,

    i64 DR0,
    i64 DR1,
    i64 DR2,
    i64 DR3,
    i64 DR6,
    i64 DR7,

    i64 RAX,
    i64 RCX,
    i64 RDX,
    i64 RBX,
    i64* RSP,
    i64* RBP,
    i64 RSI,
    i64 RDI,
    i64 R8,
    i64 R9,
    i64 R10,
    i64 R11,
    i64 R12,
    i64 R13,
    i64 R14,
    i64 R15,
	
	void* RIP
}

struct WindowsExceptionPointers {
	WindowsExceptionRecord* ExceptionRecord,
	WindowsContextRecord* ContextRecord
}

define void PushX64Volitile() asm {
	pop, rax
	push, rbx
	push, rdi
	push, rsi
	push, r12
	push, r13
	push, r14
	push, r15
	jmp, rax
}
define void PopX64Volitile() asm {
	pop, rax
	pop, r15
	pop, r14
	pop, r13
	pop, r12
	pop, rsi
	pop, rdi
	pop, rbx
	jmp, rax
}

define i32 WindowsExceptionFilter(WindowsExceptionPointers* ExceptionInfo) x64 {
	PushX64Volitile()
	
	WindowsContextRecord* Context := ExceptionInfo->ContextRecord
	StackFrame* Handler := FindHandler(Context->RBP)
	
	Context->RDI := ExceptionInfo->ExceptionRecord->ExceptionCode
	
	if (Handler = null) {
		Context->RIP := &CallUnhandledExceptionHandler
	}
	else {
		Context->RBP := Handler As i64*
		Context->RSP := (Handler->LastFrame - 1) As i64*
		Context->RIP := Handler->ReturnAddress
	}
	
	PopX64Volitile()
	
	return -1
}

struct SigInfo {
	i32 SignalNumber
	i32 ErrorNumber
	i32 SignalCode
	i32 TrapNumber
	
	void* Address
}

struct SigContext {
	i64 R8
	i64 R9
	i64 R10
	i64 R11
	i64 R12
	i64 R13
	i64 R14
	i64 R15
	i64 RDI
	i64 RSI
	i64* RBP
	i64 RBX
	i64 RDX
	i64 RAX
	i64 RCX
	i64* RSP
	i64* RIP
	i64 RFlags
}

struct SigUserContext {
	i64 Flags
	i64 linked
	i64 StackPointer
	i64 StackFlags
	i64 StackSize
	SigContext Registers
}

void* SIG_DEFAULT := 0 As void*
void* SIG_IGNORE := 1 As void*

i32 SA_SIGINFO := 0x04
i32 SA_RESTORER := 0x400_0000

struct SigAction {
	void* Handler
	i64 Flags
	void* Restorer
	i64[8] Mask
}

define void SigRestorer() asm {
	mov, rax, 15
	syscall
}

define void SigHandler(i32 SignalNumber, SigInfo* Info, SigUserContext* UserContext) {
	SigContext* Context := UserContext~>Registers
	
	StackFrame* Handler := FindHandler(Context->RBP)
	
	Context->RDI := Info->SignalNumber
	
	if (Handler = null) {
		Context->RIP := &CallUnhandledExceptionHandler
	}
	else {
		Context->RBP := Handler As i64*
		Context->RSP := (Handler->LastFrame - 1) As i64*
		Context->RIP := Handler->ReturnAddress
	}
}

SigAction SignalToExceptionHandler {
	Handler: &SigHandler, 
	Restorer: &SigRestorer, 
	Flags: SA_RESTORER | SA_SIGINFO
}

i32 SIGILL  := 4
i32 SIGBUS  := 7
i32 SIGSEGV := 11

DllImport i8 SetErrorMode(i32) {Kernel32.dll, SetErrorMode}
DllImport void* SetUnhandledExceptionFilter(void*) {Kernel32.dll, SetUnhandledExceptionFilter}
DllImport void* AddVectoredExceptionHandler(i32, void*) {Kernel32.dll, AddVectoredExceptionHandler}

define void* SetupExceptionTranslation() {
	windows {
		SetErrorMode(1)
		
		SetUnhandledExceptionFilter(&WindowsExceptionFilter)
		AddVectoredExceptionHandler(1, &WindowsExceptionFilter)
	}
	linux {
		syscall:(13, SIGILL, &SignalToExceptionHandler, null, 8)
		syscall:(13, SIGBUS, &SignalToExceptionHandler, null, 8)
		syscall:(13, SIGSEGV, &SignalToExceptionHandler, null, 8)
	}
	
	return null
}

void* DummyNameForExceptionTranslation := SetupExceptionTranslation()