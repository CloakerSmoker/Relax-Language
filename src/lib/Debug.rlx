#Require "./src/lib/Console.rlx"
#Require "./src/lib/Exception.rlx"

define void PrintStackDump(i32 IgnoreFrames) {
	StackFrame* Last := GetRBP() As StackFrame*
	i32 Index := 1
	
	while (Last) {
		if (Last->LastFrame & 1) {
			StackFrame* Temp := Last->LastFrame - 1
			
			if (IgnoreFrames) {
				IgnoreFrames -= 1
			}
			else {
				Print("	^10Stack[%i]^F0 ^D0Try catch address^F0 = %x\n", Index, Last->ReturnAddress, Temp)
				Index += 1
			}
			
			Last := Temp
		}
		else {
			if (IgnoreFrames) {
				IgnoreFrames -= 1
			}
			else {
				i64* MaybeMagic := (Last - 0x8) As i64*
				i8** MaybeName := (Last - 0x10) As i8**
				
				if (Debug && *MaybeMagic = 0x12345678) {
					Print("	^10Stack[%i]^F0 ^B0Fn^F0 = %s\n", Index, *MaybeName)
				}
				else {
					Print("	^10Stack[%i]^F0 ^B0Fn callsite^F0 = %x\n", Index, Last->ReturnAddress, Last->LastFrame)
				}
				
				Index += 1
			}
			
			Last := Last->LastFrame
		}
	}
}
define void PrintStackDump() {
	PrintStackDump(0)
}

define void UnhandledExceptionHandler(i64 Code) {
	Print("\n\n^40----------------------^F0\n")
	Print("^40Unhandled exception ^F0%x^40 with call stack:^F0\n", Code)
	PrintStackDump(3)
	Print("^40Aborting...^F0\n")
	Print("^40----------------------^F0\n\n\n")
}

define void* DebugInsertUnhandledExceptionHandler() {
	UnhandledExceptionHandler := &UnhandledExceptionHandler
	
	if !(Debug) {
		Print("^60Warning: Debug library included without '--debug' compiler flag, stack traces will only contain raw addresses.^F0\n")
	}
	
	; Only included so you can call `SetWorkingDirectory` from GDB when it refuses to `cd` to the correct place.
	;  (even if the actual program doesn't call SetWorkingDirectory)
	
	SetWorkingDirectory(".")
	
	return null
}

void* DummyDebugHandler := DebugInsertUnhandledExceptionHandler()